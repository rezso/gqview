AC_INIT(src/main.c)
AC_CONFIG_HEADER(config.h)

AM_INIT_AUTOMAKE(gqview, 2.1.5)

AM_MAINTAINER_MODE

AC_ISC_POSIX
AC_PROG_CC
AC_STDC_HEADERS
AC_ARG_PROGRAM

dnl reasonable guesses for where stuff is installed
if test "x$prefix" = "xNONE"; then
  prefix="/usr/local"
else
  prefix=$prefix
fi

AM_PATH_GTK_2_0(2.4.0,,AC_MSG_ERROR(GTK+ >= 2.4.0 not installed.))

AC_DEFINE_UNQUOTED(GQVIEW_HELPDIR, "$prefix/share/doc/gqview-$VERSION", [Location of documentation files])
AC_DEFINE_UNQUOTED(GQVIEW_HTMLDIR, "$prefix/share/doc/gqview-$VERSION/html", [Location of html documentation])

dnl checks for functions
AC_CHECK_FUNCS(strverscmp)

dnl check for little cms (lcms, this test pulled from gimp)
AC_ARG_WITH(lcms, [  --without-lcms          build without lcms support])

have_lcms=no
if test "x$with_lcms" != "xno"; then
  AC_CHECK_LIB(lcms, cmsCreate_sRGBProfile, [
    AC_CHECK_HEADER(lcms.h,
      have_lcms=yes, [
      AC_CHECK_HEADER(lcms/lcms.h,
        have_lcms=yes
        AC_DEFINE(HAVE_LCMS_LCMS_H, 1,
          [Define to 1 if the lcms header must be included as lcms/lcms.h]))
      ])
  ])
  if test "$have_lcms" = "yes"; then
    LCMS_LIBS="-llcms"
    AC_DEFINE(HAVE_LCMS, 1, [define to enable use of color profiles with lcms])
  else
    have_lcms="no (lcms not found or unusable)"
  fi
else
  have_lcms="no (lcms support disabled)"
fi

AC_SUBST(LCMS_LIBS)
AM_CONDITIONAL(HAVE_LCMS, test "$have_lcms" = "yes")

ALL_LINGUAS="ar be bg ca cs da de eo es et eu fi fr hu id it ja ko nl no pl pt_BR ro ru sk sl sv th tr uk vi zh_CN.GB2312 zh_TW"
GETTEXT_PACKAGE=$PACKAGE
AC_SUBST(GETTEXT_PACKAGE)
AM_GLIB_GNU_GETTEXT
AM_GLIB_DEFINE_LOCALEDIR(GQVIEW_LOCALEDIR)

AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

AC_OUTPUT([
Makefile
src/Makefile
src/icons/Makefile
src/icons/svg/Makefile
po/Makefile.in
doc/Makefile
gqview.spec
])

